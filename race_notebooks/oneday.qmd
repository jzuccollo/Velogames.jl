---
title: "One-day"
---

```{julia}
using Velogames, DataFrames, Statistics, StatsBase

raceurl = "https://www.velogames.com/sixes-superclasico/2023/riders.php"
betfairurl = "https://www.betfair.com/sport/cycling/world-championships/12064071/mens-road-race-winner/924.369154252"
racehash = "#WorldChamps"
racesex = "me"
racecategory = "one-day-races"
```

# Retrieve data

Data is scraped from the [Velogames website](https://www.velogames.com/). Rider scores are the number of points they have accumulated in the current season.

```{julia}
ridervg = getvgriders(raceurl)

# keep only riders in the startlist for for the race
ridervg = ridervg[ridervg.startlist.==racehash, :]

# get PCS rankings
rideroneday = getpcsranking(racesex, racecategory)

# get Betfair odds
riderodds = getodds(betfairurl)
```

## Combine datasets

```{julia}
# join all three datasets on riderkey
riderdf = leftjoin(ridervg, rideroneday, on=:riderkey, makeunique=true)
riderdf = leftjoin(riderdf, riderodds, on=:riderkey, makeunique=true)

# rename points to vgpoints
rename!(
    riderdf,
    :points => :vgpoints,
    :cost => :vgcost,
    :rank => :pcsrank,
    :points_1 => :pcspoints
)
# drop columns ending in _1
select!(riderdf, Not([:rider_1, :rider_2, :team_1]))

# fill in missing values in rank and points with 0
riderdf[ismissing.(riderdf.pcsrank), :pcsrank] .= 0
riderdf[ismissing.(riderdf.pcspoints), :pcspoints] .= 0
riderdf[ismissing.(riderdf.odds), :odds] .= 500

# convert odds to probability of win
riderdf.invodds = 1 ./ riderdf.odds

disallowmissing!(riderdf)

# normalise odds, vgpoints, pcspoints
riderdf.zinvodds = StatsBase.zscore(riderdf.invodds)
riderdf.zvgpoints = StatsBase.zscore(riderdf.vgpoints)
riderdf.zpcspoints = StatsBase.zscore(riderdf.pcspoints)
```

## Calculate rider score and value

```{julia}
# weights for inv odds, vgpoints, pcspoints
WEIGHTS = [1, 1, 1]

# calculate rider calcscore as the mean of the velogames score and the PCS score
zweights = WEIGHTS ./ sum(WEIGHTS)
riderdf.calcscore = zweights[1] .* riderdf.zinvodds .+ zweights[2] .* riderdf.zvgpoints .+ zweights[3] .* riderdf.zpcspoints

# calculate rider value as the ratio of rider score to rider cost
riderdf.calcvalue = riderdf.calcscore ./ riderdf.vgcost

riderdf
```

# Inspect the data

## Top 10 riders by points

```{julia}
sort(riderdf, :calcscore, rev=true)[1:10, :]
```

## Top 10 riders by value

```{julia}
sort(riderdf, :calcvalue, rev=true)[1:10, :]
```

# Results

## Total cost of team

Must be no more than 100.

```{julia}
modelresults = buildmodeloneday(riderdf, 6, :calcscore, :vgcost)

# total cost
riderdf.vgcost .* modelresults.data |> sum
```

## Selected riders

```{julia}
# selected riders
riderdf[!, :chosen] = modelresults.data .|> !iszero
chosenteam = filter(row -> row.chosen, riderdf)
chosenteam[:, [:rider, :team, :vgcost, :vgpoints, :pcspoints, :pcsrank, :odds, :calcscore, :calcvalue]]
```
